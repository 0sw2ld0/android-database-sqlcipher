apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply from: "native.gradle"

android {

  compileSdkVersion 26
  buildToolsVersion "26.0.1"

  defaultConfig {
    minSdkVersion "${minimumAndroidSdkVersion}"
    targetSdkVersion "${targetAndroidSdkVersion}"
    versionCode 1
    versionName "${clientVersionNumber}"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  sourceSets {
    main {
      jniLibs.srcDirs "${rootProject.ext.nativeRootOutputDir}/libs"
    }
  }

  clean.dependsOn cleanNative

  preBuild.dependsOn([buildOpenSSL, buildAmalgamation])

  afterEvaluate {
    android.libraryVariants.all { variant ->
      variant.javaCompile.dependsOn(buildNative)
      variant.outputs.each { output ->
        def outputFile = output.outputFile
        if (outputFile != null && outputFile.name.endsWith(".aar")) {
          def fileName = "${archivesBaseName}-${clientVersionNumber}.aar"
          output.outputFile = new File(outputFile.parent, fileName)
        }
      }
    }
  }

}

publishing {
  publications {
    sqlcipher(MavenPublication) {
      groupId "net.zetetic"
      artifactId "android-database-sqlcipher"
      version "${clientVersionNumber}"
      artifact("$buildDir/outputs/aar/${archivesBaseName}-${clientVersionNumber}.aar")
      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name "android-database-sqlcipher"
          description "SQLCipher for Android is a plugin to SQLite that provides full database encryption."
          url "https://www.zetetic.net/sqlcipher"
          scm {
            url "https://github.com/sqlcipher/android-database-sqlcipher.git"
            connection "scm:git:https://github.com/sqlcipher/android-database-sqlcipher.git"
            developerConnection "scm:git:https://github.com/sqlcipher/android-database-sqlcipher.git"
          }
          licenses {
            license {
              url "https://www.zetetic.net/sqlcipher/license/"
            }
          }
          developers {
            developer {
              name "Zetetic Support"
              email "support@zetetic.net"
              organization "Zetetic LLC"
              organizationUrl "https://www.zetetic.net/"
            }
          }
          distributionManagement {
            snapshotRepository {
              id "ossrh"
              url "https://oss.sonatype.org/content/repositories/snapshots"
            }
            repository {
              id "ossrh"
              url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "$buildDir/repo"
    }
  }
}
