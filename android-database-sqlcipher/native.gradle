task buildOpenSSL () {
  onlyIf {
    def armNativeFile = new File("${androidNativeRootDir}/armeabi/libcrypto.a")
    return !armNativeFile.exists()
  }
  doLast {
    exec {
      workingDir "${projectDir}"
      commandLine "./build-openssl-libraries.sh"
    }
  }
}

task buildAmalgamation () {
  doLast {
    exec {
      workingDir "${projectDir}/src/main/external/sqlcipher"
      environment("CFLAGS", "${rootProject.ext.sqlcipherCFlags}")
      commandLine "./configure", "--enable-tempstore=yes", "--with-crypto-lib=none"
    }
    exec {
      workingDir "${projectDir}/src/main/external/sqlcipher"
      environment("CFLAGS", "${rootProject.ext.sqlcipherCFlags}")
      commandLine "make",  "sqlite3.c"
    }
  }
}

task buildNative32() {
  description "Build the 32-bit native SQLCipher binaries"
  doLast {
    executeNdkBuild(
      "${rootProject.ext.nativeRootOutputDir}/libs32",
      file("src/main/cpp").absolutePath,
      file("src/main/cpp/Application32.mk").absolutePath,
      "${rootProject.ext.sqlcipherCFlags}")
  }
}

task buildNative64() {
  description "Build the 64-bit native SQLCipher binaries"
  doLast {
    executeNdkBuild(
      "${rootProject.ext.nativeRootOutputDir}/libs64",
      file("src/main/cpp").absolutePath,
      file("src/main/cpp/Application64.mk").absolutePath,
      "${rootProject.ext.sqlcipherCFlags}")
  }
}

task cleanOpenSSL() {
  description "Clean the OpenSSL source"
  doLast {
    logger.info "Cleaning OpenSSL source"
    gitClean("${rootProject.ext.opensslDir}")
  }
}

task cleanSQLCipher() {
  description "Clean the SQLCipher source"
  doLast {
    logger.info "Cleaning SQLCipher source"
    gitClean("${rootProject.ext.sqlcipherDir}")
  }
}

task cleanNative() {
  description "Clean the native (JNI) build artifacts"
  doLast {
    logger.info "Cleaning native build artifacts"
    ["libs32", "libs64", "obj"].each {
      new File("${projectDir}/src/main/${it}").deleteDir()
    }
    new File("${androidNativeRootDir}/").deleteDir()
  }
}

task distclean(dependsOn: [clean, cleanSQLCipher, cleanOpenSSL]) {
  description "Clean build, SQLCipher, and OpenSSL artifacts"
}

def gitClean(directory) {
  logger.info "Cleaning directory:${directory}"
  exec {
    workingDir "${directory}"
    commandLine "git", "checkout", "-f"
  }
}

def executeNdkBuild(outputDir, androidMkDirectory, applicationMkFile, cflags) {
  logger.info "Executing NDK build command"
  exec {
    def outputDirectory = "NDK_LIBS_OUT=${outputDir}"
    def applicationFile = "NDK_APPLICATION_MK=${applicationMkFile}"
    environment("SQLCIPHER_CFLAGS", "${cflags}")
    commandLine "ndk-build", "--environment-overrides", outputDirectory,
    "-C", androidMkDirectory, applicationFile
  }
}
